import java.text.SimpleDateFormat

/*
 * Copyright (c) 2017 by Tran Le Duy
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.library'
apply plugin: 'me.tatarka.retrolambda'

static def buildDate() {
    Date date = new Date();
    SimpleDateFormat dateFormat = new SimpleDateFormat("ddMMyyyy")
    return dateFormat.format(date)
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        multiDexEnabled true
        versionCode 19
        versionName "1.1.8"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            resources.srcDirs = ['src/main/resources']
            java.srcDirs += 'matheclipse-core/src/main/java/'
        }
        test {
            java.srcDirs += "matheclipse-core/src/test/java/"
            java.srcDirs += "tools/src/main/java/"
        }
        androidTest {
            java.srcDirs = ['matheclipse-core/src/androidTest/java/']
        }
    }

    //avoid lint check, real binary not contains java 8
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

repositories {
    google()
    jcenter()
    mavenCentral()
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.jsoup:jsoup:1.11.2'
}


retrolambda {
    javaVersion JavaVersion.VERSION_1_7
    defaultMethods true
    incremental false
    jvmArgs '-noverify'
}

ext {

    //For bintray and jcenter distribution
    bintrayRepo = 'Symja7'
    bintrayName = 'org.matheclipse.android'

    publishedGroupId = 'org.matheclipse.android'
    libraryName = 'symja7'
    artifact = 'symja7'
    libraryVersion = '1.0.0'

    libraryDescription = 'Java computer algebra and symbolic math library'

    siteUrl = 'https://github.com/tranleduy2000/symja_android_library'
    gitUrl = 'https://github.com/tranleduy2000/symja_android_library.git'


    developerId = 'tranleduy2000'
    developerName = 'Duy Tran'
    developerEmail = 'tranleduy1233@gmail.com'
    organization = 'tranleduy2000'
    licenseName = 'GNU GENERAL PUBLIC LICENSE, Version 3.0'
    licenseUrl = 'https://www.gnu.org/licenses/gpl-3.0.html'
    allLicenses = ["GNU-GPL-3.0"]

}
//These two remote files contain Bintray configuration as described above.
apply from: 'install.gradle'
apply from: 'bintray.gradle'